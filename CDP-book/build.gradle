plugins {
  id "us.kirchmeier.capsule" version "1.0.2"
}

apply plugin: "java"
apply plugin: "maven"
apply plugin: "eclipse-wtp"

group "com.github.michael-joyner"
version '20161123'

sourceCompatibility = 1.8
compileJava.options.encoding = 'UTF-8'
  
repositories {
        jcenter()
        maven { url "https://jitpack.io" }
        maven {
        	url "https://github.com/psiegman/mvn-repo/raw/master/releases"
        }
    }
    
task fatjar(type: FatCapsule) {
  	archiveName 'CDP-book.jar'
	applicationClass 'net.cherokeedictionary.main.Main'
	capsuleManifest {
    	jvmArgs = ['-Dfile.encoding=UTF-8']
	}
}
    
dependencies {
		compile 'commons-codec:commons-codec:+'
        compile 'commons-io:commons-io:+'
        compile 'org.apache.commons:commons-lang3:+'
        compile 'org.apache.commons:commons-text:+'
        compile 'org.apache.commons:commons-csv:+'
        
        //jitpack.io sourced libs
        compile 'com.github.michael-joyner:SimpleTextUi:-SNAPSHOT'
		compile 'com.github.michael-joyner:CherokeeDictionaryShared:-SNAPSHOT'
		compile 'com.github.michael-joyner:CherokeeDictionaryDao:-SNAPSHOT'
		compile 'com.github.michael-joyner:CherokeeLanguageUtils:-SNAPSHOT'
    }

configurations.all {
  resolutionStrategy {
  	force 'xml-apis:xml-apis:1.4.01'
  }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 30, 'seconds'
}

task "create-dirs" << {
	sourceSets*.java.srcDirs*.each { it.mkdirs() }
	sourceSets*.resources.srcDirs*.each { it.mkdirs() }
}

eclipseJdt << {
    File f = file('.settings/org.eclipse.core.resources.prefs')
    f.write('eclipse.preferences.version=1\n')
    f.append('encoding/<project>=utf-8')
    f = file('.settings/org.eclipse.core.runtime.prefs')
    f.write('eclipse.preferences.version=1\n')
    f.append('line.separator=\\n\n')
}

project.afterEvaluate {
	// use jre lib matching version used by project, not the workspace default
	if (sourceCompatibility != null) {
		def target = project.sourceCompatibility.toString()
		def containerPrefix = "org.eclipse.jdt.launching.JRE_CONTAINER"
		def containerSuffix
		if (target =~ /1.[4-5]/) {
			containerSuffix = '/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/J2SE-' + target
		} else if (target =~ /1.[6-8]/) {
			containerSuffix = '/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/JavaSE-' + target
		}
		if (containerSuffix != null) {
			project.eclipse.classpath {
				containers.removeAll { it.startsWith(containerPrefix) }
				containers.add(containerPrefix + containerSuffix)
			}
		}
	}
}

eclipse {
	project {
		name = 'CDP-book'
		comment = 'CDP-book'
		natures 'org.eclipse.buildship.core.gradleprojectnature'
		referencedProjects
		//'CherokeeDictionaryDao', 'SimpleTextUi', 'CherokeeLanguageUtils'
		//webAppDirName = 'src/main/webapp'
	}

	classpath {
		downloadSources = true
		downloadJavadoc = true
	}

	jdt {
		sourceCompatibility=1.8
		targetCompatibility=1.8
	}

	wtp {
		facet {
			facets = []
			facet name: 'jst.java', version: '1.8'
			/*facet name: 'jst.web', version: '3.0'*/
		}
	}
}
